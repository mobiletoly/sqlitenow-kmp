import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

plugins {
    id("org.jetbrains.kotlin.jvm") version "2.2.20"
    id("com.vanniktech.maven.publish") version "0.34.0"
    id("java-gradle-plugin")
}

group = "dev.goquick.sqlitenow"
version = "0.2.5-SNAPSHOT"

repositories {
    mavenCentral()
    google()
}

kotlin {
    jvmToolchain(21)

    // Set language version to 2.2 to support break/continue in inline lambdas
    compilerOptions {
        languageVersion.set(KotlinVersion.KOTLIN_2_2)
        freeCompilerArgs.addAll("-Xmulti-dollar-interpolation", "-Xcontext-parameters")
    }
}

gradlePlugin {
    plugins {
        sqliteNow {
            id = "dev.goquick.sqlitenow"
            displayName = "SqliteNow Code Generator"
            description = "Generates Kotlin files for SqliteNow"
            implementationClass = "dev.goquick.sqlitenow.gradle.SqliteNowPlugin"
        }
    }
}

dependencies {
    implementation(gradleApi())
    implementation(localGroovy())
    implementation("com.squareup:kotlinpoet:2.2.0")
    compileOnly("org.jetbrains.kotlin:kotlin-gradle-plugin:2.2.0")
    implementation("com.typesafe:config:1.4.3")
    implementation("org.slf4j:slf4j-simple:2.0.10")
    // SQLite JDBC for database access
    implementation("org.xerial:sqlite-jdbc:3.50.3.0")
    implementation("com.github.jsqlparser:jsqlparser:4.9")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0")

    // -- Testing dependencies --
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.13.4")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.13.4")
    testImplementation("org.jetbrains.kotlin:kotlin-test:2.2.20")
    testImplementation("org.mockito:mockito-core:5.19.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.19.0")
}

testing {
}

tasks.test {
    useJUnitPlatform()
}

mavenPublishing {
    publishToMavenCentral()

    signAllPublications()

    coordinates(group.toString(), "plugin", version.toString())

    pom {
        name = "SQLiteNow Gradle Plugin"
        description = "Gradle plugin for generating Kotlin interfaces for SQLite files"
        inceptionYear = "2025"
        url = "https://github.com/mobiletoly/sqlitenow-kmp/"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "repo"
            }
        }
        developers {
            developer {
                id = "mobiletoly"
                name = "Toly Pochkin"
                url = "https://github.com/mobiletoly"
            }
        }
        scm {
            url = "https://github.com/mobiletoly/sqlitenow-kmp"
            connection = "scm:git:git://github.com/mobiletoly/sqlitenow-kmp.git"
            developerConnection = "scm:git:git://github.com/mobiletoly/sqlitenow-kmp.git"
        }
    }
}
