import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

plugins {
    id("org.jetbrains.kotlin.jvm") version "2.3.0"
    id("com.vanniktech.maven.publish") version "0.35.0"
    id("java-gradle-plugin")
}

group = "dev.goquick.sqlitenow"
version = "0.6.0"

repositories {
    mavenCentral()
    google()
}

kotlin {
    jvmToolchain(17)

    // Set language version to 2.2 to support break/continue in inline lambdas
    compilerOptions {
        languageVersion.set(KotlinVersion.KOTLIN_2_3)
        freeCompilerArgs.addAll("-Xmulti-dollar-interpolation", "-Xcontext-parameters")
    }
}

gradlePlugin {
    plugins {
        sqliteNow {
            id = "dev.goquick.sqlitenow"
            displayName = "SqliteNow Code Generator"
            description = "Generates Kotlin files for SqliteNow"
            implementationClass = "dev.goquick.sqlitenow.gradle.SqliteNowPlugin"
        }
    }
}

dependencies {
    implementation(gradleApi())
    implementation(localGroovy())
    implementation("com.squareup:kotlinpoet:2.2.0")
    compileOnly("org.jetbrains.kotlin:kotlin-gradle-plugin:2.2.0")
    implementation("com.typesafe:config:1.4.5")
    implementation("org.slf4j:slf4j-simple:2.0.17")
    // SQLite JDBC for database access
    implementation("org.xerial:sqlite-jdbc:3.51.1.0")
    implementation("com.github.jsqlparser:jsqlparser:4.9")
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.10.0")

    // -- Testing dependencies --
    testImplementation("org.junit.jupiter:junit-jupiter-api:6.0.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:6.0.2")
    testImplementation("org.jetbrains.kotlin:kotlin-test:2.3.0")
    testImplementation("org.mockito:mockito-core:5.21.0")
    testImplementation("org.mockito:mockito-junit-jupiter:5.21.0")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2")
}

testing {
}

tasks.test {
    useJUnitPlatform()
}

mavenPublishing {
    publishToMavenCentral()

    def isPublishingToMavenLocal = gradle.startParameter.taskNames.any { it.toLowerCase().contains("publishtomavenlocal") }
    def hasSigningCredentials =
            System.getenv("SIGNING_KEY") ||
                    System.getenv("SIGNING_KEY_ID") ||
                    System.getenv("SIGNING_PASSWORD") ||
                    project.findProperty("signingInMemoryKey") ||
                    project.findProperty("signing.keyId") ||
                    project.findProperty("signing.password") ||
                    project.findProperty("signing.secretKeyRingFile") ||
                    project.findProperty("signing.gnupg.keyName")

    if (!isPublishingToMavenLocal && hasSigningCredentials) {
        signAllPublications()
    }

    coordinates(group.toString(), "plugin", version.toString())

    pom {
        name = "SQLiteNow Gradle Plugin"
        description = "Gradle plugin for generating Kotlin interfaces for SQLite files"
        inceptionYear = "2025"
        url = "https://github.com/mobiletoly/sqlitenow-kmp/"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "repo"
            }
        }
        developers {
            developer {
                id = "mobiletoly"
                name = "Toly Pochkin"
                url = "https://github.com/mobiletoly"
            }
        }
        scm {
            url = "https://github.com/mobiletoly/sqlitenow-kmp"
            connection = "scm:git:git://github.com/mobiletoly/sqlitenow-kmp.git"
            developerConnection = "scm:git:git://github.com/mobiletoly/sqlitenow-kmp.git"
        }
    }
}
